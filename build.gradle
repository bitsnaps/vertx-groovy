apply plugin: 'java'

ext {
    vertxVersion = "3.5.4"
}

task annotationProcessing(type: JavaCompile, group: 'build') {
    source = sourceSets.main.java
    classpath = configurations.compile + configurations.compileOnly
    destinationDir = project.file('src/main/generated')
    options.compilerArgs = [
            "-proc:only",
            "-processor", "io.vertx.codegen.CodeGenProcessor",
            "-Acodegen.output=${destinationDir.absolutePath}"
    ]
}

compileJava {
    targetCompatibility = 1.8
    sourceCompatibility = 1.8

    dependsOn annotationProcessing
}

sourceSets {
    main {
        java {
            srcDirs += 'src/main/generated'
        }
    }
}

repositories {
    jcenter()
}

dependencies {

    compile "io.vertx:vertx-core:${vertxVersion}"
    compile "io.vertx:vertx-web:${vertxVersion}"
    // not used in this branch
//    compile "io.vertx:vertx-auth-common:${vertxVersion}"

    // not used in this app
    compile "io.vertx:vertx-jdbc-client:${vertxVersion}"
    compileOnly "io.vertx:vertx-codegen:${vertxVersion}"
//    compile 'mysql:mysql-connector-java:6.0.5'


    compile 'org.slf4j:slf4j-simple:1.7.22'
    compile 'org.slf4j:slf4j-api:1.7.22'

    // testing
    testCompile "io.vertx:vertx-unit:${vertxVersion}"
    testCompile 'junit:junit:4.12'

}

jar {
    // by default fat jar
    archiveName = 'vertx-hello-fat.jar'
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    manifest {
        attributes 'Main-Class': 'io.vertx.core.Launcher'
        attributes 'Main-Verticle': 'Hello'
    }
}

